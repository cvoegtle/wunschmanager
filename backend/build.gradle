buildscript {    // Configuration for building
    ext.kotlin_version = '1.7.0'
    ext.spring_boot_version = '2.7.2'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.cloud.tools:appengine-gradle-plugin:+'    // latest App Engine Gradle tasks
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'war'
apply plugin: 'com.google.cloud.tools.appengine'  // App Engine tasks

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    providedCompile 'javax.servlet:javax.servlet-api:4.0.1'
    implementation 'com.google.appengine:appengine:1.9.98'
    implementation 'com.google.appengine:appengine-api-1.0-sdk:2.0.5'
    implementation 'com.google.appengine:appengine-api-labs:1.9.88'
    implementation 'com.google.appengine:appengine-endpoints:1.9.86'
    implementation 'com.google.appengine:appengine-endpoints-deps:1.9.86'
    implementation 'com.google.appengine:appengine-jsr107cache:1.9.98'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.13.3'

    implementation 'com.google.guava:guava:31.1-jre'
    implementation 'com.googlecode.objectify:objectify:5.1.21'

    implementation "org.springframework.boot:spring-boot-starter-web:$spring_boot_version"
    providedRuntime "org.springframework.boot:spring-boot-starter-tomcat:$spring_boot_version"
    testImplementation "org.springframework.boot:spring-boot-starter-test:$spring_boot_version"
}

war {
    webInf { from 'web/WEB-INF' }
}


// [START model]
appengine {  // App Engine tasks configuration
    deploy {
        version = '49'
        projectId = 'wunschmanager'
        stopPreviousVersion = true
        promote = true
    }
}

def webappDir = "$projectDir/src/main/webapp"

task explodedWar(type: Sync) {
    into "${buildDir}/exploded"
    with war
}

clean {
    dependsOn "cleanupWebApp"
}


task cleanupWebApp(type:Delete) {
    delete fileTree("$webappDir/assets") {
        include "*"
    }
    delete "$webappDir/assets"
    delete fileTree("$webappDir") {
        include "*"
    }
}

repositories {
    mavenCentral()
}
